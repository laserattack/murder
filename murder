#!/usr/bin/env perl

use strict;
use warnings;
use feature 'say';
use Time::HiRes 'sleep';
use Getopt::Long;

our @SIGNALS = (
    # [signal, timeout]
    [15, 3],
    [2, 3],
    [1, 4],
    [9, 0]
);

our $COLOR = "\e[36m";
our $RESET = "\e[0m";

# flags
our $FORCE   = 0;
our $QUIET   = 0;
our $HELP    = 0;
our $NOCOLOR = 0;

sub go_ahead {
    my $input = <STDIN>;
    chomp($input);
    $input =~ s/^\s+|\s+$//g;
    $input = lc($input);

    return 1 if $input eq '';
    return $input =~ /^(y|yes|yas)$/ ? 1 : 0;
}

sub is_running {
    my ($pid) = @_;
    return `ps -p $pid` =~ tr/\n// == 2;
}

sub get_all_descendants {
    my ($pid) = @_;
    my %result;

    my $find_children;
    $find_children = sub {
        my ($parent) = @_;
        my @children = split(/\n/, `ps -eo pid,ppid | awk '\$2 == $parent {print \$1}'`);
        foreach my $child (@children) {
            next if $result{$child}++;
            $find_children->($child);
        }
    };

    $find_children->($pid);
    return keys %result;
}

sub murder_pid {
    my ($pid) = @_;

    my @all_pids = get_all_descendants($pid);
    unshift @all_pids, $pid;

    if (!$QUIET) {
        say "Process tree: " . join($RESET.', '.$COLOR, map { $COLOR.$_ } @all_pids).$RESET;
    }

    foreach my $target_pid (reverse @all_pids) {
        next unless is_running($target_pid);

        foreach my $sig (@SIGNALS) {
            my ($code, $wait) = @$sig;
            if (!$QUIET) {
                say "Sending signal ".$COLOR."$code".$RESET." to pid ".$COLOR."$target_pid".$RESET;
            }
            kill($code, $target_pid);
            sleep(0.3);
            last unless is_running($target_pid);
            sleep($wait);
        }
    }
}

sub murder_port {
    my ($port) = @_;
    $port =~ s/^://;
    my @pids = split(/\n/, `lsof -ti:$port`);
    foreach my $pid (@pids) {
        my $command = `ps -p $pid -o comm=`;
        chomp($command);
        if ($FORCE) {
            murder_pid($pid);
        } else {
            print("Kill process ".$COLOR."$command".$RESET." (pid ".$COLOR."$pid".$RESET.")? [Y/n] ");
            if (go_ahead()) {
                murder_pid($pid);
            }
        }
    }
}

sub murder_names {
    my ($name) = @_;
    my $running = `ps -eo 'pid command' | grep -Fiw '$name' | grep -Fv grep`;
    foreach my $line (split(/\n/, $running)) {
        my ($pid, $command) = $line =~ /^\s*(\d+)\s+(.+?)\s*$/;
        next if $$ == $pid;
        if ($FORCE) {
            murder_pid($pid);
        } else {
            print("Kill process ".$COLOR."$command".$RESET." (pid ".$COLOR."$pid".$RESET.")? [Y/n] ");
            if (go_ahead()) {
                murder_pid($pid);
            }
        }
    }
}

sub murder {
    my ($arg) = @_;
    if ($arg =~ /^\d+$/) {
        murder_pid($arg);
    } elsif ($arg =~ /^:\d+$/) {
        murder_port($arg);
    } else {
        murder_names($arg);
    }
}

sub main {
    if (@ARGV < 1) {
        usage();
    } else {
        foreach my $arg (@ARGV) {
            murder($arg);
        }
    }
}

sub usage {
    say "Usage: murder [OPTIONS] <PID|NAME|:PORT>";
    say "";
    say "Options:";
    say "  -f, --force     Kill without confirmation";
    say "  -q, --quiet     Quiet mode (minimal output)";
    say "  -h, --help      Show this help message";
    say "      --nocolor   Disable colored output";
    say "";
    say "Arguments:";
    say "  PID             Process ID to kill";
    say "  NAME            Process name to kill (e.g., ruby, python)";
    say "  :PORT           Port number to kill process on (e.g., :3000)";
    say "";
    say "Examples:";
    say "  murder 1234               Kill process with PID 1234";
    say "  murder -f ruby            Kill all ruby processes without asking";
    say "  murder -q :3000           Kill process on port 3000 quietly";
    say "  murder --nocolor python   Kill python processes without colors";
}

GetOptions(
    'force|f' => \$FORCE,
    'quiet|q' => \$QUIET,
    'help|h'  => \$HELP,
    'nocolor' => \$NOCOLOR,
    ) or do {
        usage();
        exit 1;
};

if ($HELP) {
    usage();
    exit 1;
}

if ($NOCOLOR) {
    $COLOR="";
    $RESET="";
}

main();
