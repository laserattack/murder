#!/usr/bin/env perl

use strict;
use warnings;
use feature 'say';
use Time::HiRes 'sleep';
use Getopt::Long;


our @SIGNALS = (
    # [signal, timeout]
    [15, 3],
    [2, 3],
    [1, 4],
    [9, 0]
);

# flags
Getopt::Long::Configure('bundling'); # can combine flags e.g., -qf
our $FORCE   = 0;
our $QUIET   = 0;
our $HELP    = 0;
our $ONE     = 0;

sub go_ahead {
    my $input = <STDIN>;
    chomp($input);
    $input =~ s/^\s+|\s+$//g;
    $input = lc($input);

    return 1 if $input eq '';
    return $input =~ /^(y|yes|yas)$/ ? 1 : 0;
}

sub is_running {
    my ($pid) = @_;
    return `ps -p $pid` =~ tr/\n// == 2;
}

sub get_all_descendants {
    my ($pid) = @_;
    my %result;

    my $find_children;
    $find_children = sub {
        my ($parent) = @_;
        my @children = split(/\n/, `ps -eo pid,ppid | awk '\$2 == $parent {print \$1}'`);
        foreach my $child (@children) {
            next if $result{$child}++;
            $find_children->($child);
        }
    };

    $find_children->($pid);
    return keys %result;
}

sub kill_single_process {
    my ($pid) = @_;
    foreach my $sig (@SIGNALS) {
        my ($code, $wait) = @$sig;
        say "Sending signal $code to pid $pid" if !$QUIET;
        kill($code, $pid);
        sleep(0.3);
        last unless is_running($pid);
        sleep($wait);
    }
}

sub murder_pid {
    my ($pid) = @_;

    if ($ONE) {
        say "Process: $pid" if !$QUIET;
        kill_single_process($pid);
        return;
    }

    my @all_pids = get_all_descendants($pid);
    unshift @all_pids, $pid;

    say "Process tree: " . join(', ', @all_pids) if !$QUIET;

    foreach my $target_pid (reverse @all_pids) {
        unless (is_running($target_pid)) {
            say "Process with pid $target_pid already dead" if !$QUIET;
            next;
        }
        kill_single_process($target_pid);
    }
}

sub murder_port {
    my ($port) = @_;
    $port =~ s/^://;
    my @pids = split(/\n/, `lsof -ti:$port`);
    foreach my $pid (@pids) {
        my $command = `ps -p $pid -o comm=`;
        chomp($command);
        if ($FORCE) {
            murder_pid($pid);
        } else {
            print("Kill process $command (pid $pid)? [Y/n] ");
            murder_pid($pid) if go_ahead();
        }
    }
}

sub murder_names {
    my ($name) = @_;
    my $running = `ps -eo 'pid command' | grep -Fiw '$name' | grep -Fv grep`;
    foreach my $line (split(/\n/, $running)) {
        my ($pid, $command) = $line =~ /^\s*(\d+)\s+(.+?)\s*$/;
        next if $$ == $pid;
        if ($FORCE) {
            murder_pid($pid);
        } else {
            print("Kill process $command (pid $pid)? [Y/n] ");
            murder_pid($pid) if go_ahead();
        }
    }
}

sub murder {
    my ($arg) = @_;
    if ($arg =~ /^\d+$/) {
        murder_pid($arg);
    } elsif ($arg =~ /^:\d+$/) {
        murder_port($arg);
    } else {
        murder_names($arg);
    }
}

sub main {
    if (@ARGV < 1) {
        usage();
    } else {
        foreach my $arg (@ARGV) {
            murder($arg);
        }
    }
}

sub usage {
    say "Usage: murder [OPTIONS] <PID|NAME|:PORT>";
    say "";
    say "Options:";
    say "  -f, --force  Kill without confirmation";
    say "  -q, --quiet  Quiet mode (minimal output)";
    say "  -h, --help   Show this help message";
    say "  -o, --one    Kill only the specified process, not the entire tree";
    say "";
    say "Arguments:";
    say "  PID    Process ID to kill";
    say "  NAME   Process name to kill (e.g., ruby, python)";
    say "  :PORT  Port number to kill process on (e.g., :3000)";
    say "";
    say "Examples:";
    say "  murder 1234        Kill process with PID 1234";
    say "  murder -f ruby     Kill all ruby processes without asking";
    say "  murder -q :3000    Kill process on port 3000 quietly";
    say "  murder -fq python  Kill python processes without asking and quietly";
}

GetOptions(
    'force|f'   => \$FORCE,
    'quiet|q'   => \$QUIET,
    'help|h'    => \$HELP,
    'one|o'     => \$ONE,
    ) or do {
        usage();
        exit 1;
};

if ($HELP) {
    usage();
    exit 1;
}

main();
