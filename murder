#!/usr/bin/env perl

# What is it : Linux process killer script
# Usage      : murder [-hfqonc] PID|NAME|:PORT

use strict;
use Getopt::Long;

BEGIN {
    eval "use warnings";
    eval "use Time::HiRes 'sleep'";
    if ($@) {
         *sleep = sub {
            my $seconds = shift;
            CORE::sleep($seconds) if $seconds > 0;
        };
    }
}

my @SIGNALS = (
    # [signal, timeout]
    [15, 3],
    [2, 3],
    [1, 4],
    [9, 0]
);

# flags
Getopt::Long::Configure('bundling'); # can combine flags e.g., -qf
my $FORCE    = 0;
my $QUIET    = 0;
my $HELP     = 0;
my $ONE      = 0;
my $DRYRUN   = 0;
my $NAMEONLY = 0;

sub log_message {
    print @_, "\n" unless $QUIET;
}

sub go_ahead {
    chomp(my $input = <STDIN>);
    $input    =~ s/^\s+|\s+$//g;
    $input    = lc($input);

    return 1 if $input eq '';
    return $input =~ /^(y|yes|yas)$/ ? 1 : 0;
}

sub is_running {
    my ($pid) = @_;
    my @lines = split(/\n/, `ps -p $pid 2>/dev/null`);
    return scalar @lines == 2;
}

sub get_all_descendants {
    my ($pid) = @_;
    my %result;
    my %children_of;

    my $ps_output = `ps -eo pid,ppid`;
    my @lines = split(/\n/, $ps_output);
    shift @lines;
    foreach my $line (@lines) {
        my ($child, $parent) = split(' ', $line);
        push @{$children_of{$parent}}, $child;
    }

    my $find_children;
    $find_children = sub {
        my ($parent) = @_;
        if (my $children = $children_of{$parent}) {
            foreach my $child (@$children) {
                next if $result{$child}++;
                $find_children->($child);
            }
        }
    };

    $find_children->($pid);
    return keys %result;
}

sub kill_process_single {
    return if $DRYRUN;
    my ($pid) = @_;
    foreach my $sig (@SIGNALS) {
        my ($code, $wait) = @$sig;
        log_message "Sending signal $code to pid $pid";
        kill($code, $pid);
        sleep(0.3);
        last unless is_running($pid);
        sleep($wait);
    }
}

sub kill_process_tree {
    my ($pid) = @_;

    if ($ONE) {
        log_message "Process: $pid";
        kill_process_single($pid);
        return;
    }

    my @all_pids = get_all_descendants($pid);
    unshift @all_pids, $pid;

    my @all_pids_with_names = map {
        chomp(my $name = `ps -p $_ -o comm= 2>/dev/null`);
        "$_($name)"
    } @all_pids;
    log_message "Process tree: ".join(', ', @all_pids_with_names);

    foreach my $target_pid (reverse @all_pids) {
        unless (is_running($target_pid)) {
            log_message "Process with pid $target_pid already dead";
            next;
        }
        kill_process_single($target_pid);
    }
}

sub murder_pid {
    my ($pid) = @_;
    chomp(my $name  = `ps -p $pid -o comm=`);
    print("Kill process $name (pid $pid)? [Y/n] ");
    if ($FORCE) {
        print("\n");
        kill_process_tree($pid);
    } else {
        kill_process_tree($pid) if go_ahead();
    }
}

sub murder_port {
    my ($port) = @_;
    $port      =~ s/^://;
    my @pids   = split(/\n/, `lsof -ti:$port`);
    foreach my $pid (@pids) {
        murder_pid($pid);
    }
}

sub murder_names {
    my ($name)  = @_;
    my $format  = $NAMEONLY ? 'pid comm' : 'pid command';
    my $running = `ps -eo '$format' | grep -Fiw '$name' | grep -Fv grep`;
    foreach my $line (split(/\n/, $running)) {
        my ($pid) = $line =~ /^\s*(\d+)/;
        next if $$ == $pid;
        murder_pid($pid);
    }
}

sub murder {
    my ($arg) = @_;
    if ($arg =~ /^\d+$/) {
        murder_pid($arg);
    } elsif ($arg =~ /^:\d+$/) {
        murder_port($arg);
    } else {
        murder_names($arg);
    }
}

sub main {
    if (@ARGV < 1) {
        usage();
    } else {
        foreach my $arg (@ARGV) {
            murder($arg);
        }
    }
}

sub usage {
    print "What is it : Linux process killer script", "\n";
    print "Usage      : murder [-hfqonc] PID|NAME|:PORT", "\n";
    print "", "\n";
    print "Flags:", "\n";
    print "  -h, --help       Show this help message", "\n";
    print "  -f, --force      Kill without confirmation", "\n";
    print "  -q, --quiet      Quiet mode (minimal output)", "\n";
    print "  -o, --one        Kill only the specified process, not the entire tree", "\n";
    print "  -c, --name-only  Search by process name only (not full command line)", "\n";
    print "  -n, --dry-run    Show what would be killed without actually killing", "\n";
    print "", "\n";
    print "Arguments:", "\n";
    print "  PID    Process ID to kill", "\n";
    print "  NAME   Process name to kill (e.g., python, emacs, firefox)", "\n";
    print "  :PORT  Port number to kill process on (e.g., :3000)", "\n";
    print "", "\n";
    print "Examples:", "\n";
    print "  murder 1234          Kill process with PID 1234", "\n";
    print "  murder -f ruby       Kill all ruby processes without asking", "\n";
    print "  murder -q :3000      Kill process on port 3000 quietly", "\n";
    print "  murder -fq python    Kill python processes without asking and quietly", "\n";
    print "  murder -nf firefox   Show what would be killed (search by full command)", "\n";
    print "  murder -nfc firefox  Show what would be killed (search by name only)", "\n";
    print "  murder -nfq firefox  Show what would be killed with minimal output", "\n";
}

GetOptions(
    'force|f'     => \$FORCE,
    'quiet|q'     => \$QUIET,
    'help|h'      => \$HELP,
    'one|o'       => \$ONE,
    'dry-run|n'   => \$DRYRUN,
    'name-only|c' => \$NAMEONLY,
    ) or do {
        usage();
        exit 1;
};

if ($HELP) {
    usage();
    exit 1;
}

main();
